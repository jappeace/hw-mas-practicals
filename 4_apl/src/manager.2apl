%TODO: use pc's intead of compontens
beliefs:
	wants([
		pc_case(
            unspec,
            unspec,
            unspec,
			unspec,
            unspec,
			unspec,
            unspec
		),
		processor(
			intel,
			'Intel Celeron A',
			2.0,
			128,
			unspec
		),
		motherboard(
			intel,
			'Intel Desktop Board D865PERL',
			'Intel 865PE',
			atx,
			['celeron', 'Pentium 4'],
			unspec
		),
		graphic_card(
			radeon,
			'ATI RADEON 7000 Graphics card',
			'Graphics Card',
			unspec
		)
	]).

	announce :- waiting(_).
	% , = and, because reasons
	awardContract :- not offer(_,_) , not best_offer(_,_).


beliefupdates:
	{waiting(TIME)} Wait() {not waiting(TIME), waiting(TIME+1)}


goals:
	announce.

pgrules:
    % the manager agent annouce his requierments to all contractor agents 
    % call for proposal step in C-Net
    announce <- wants(PARTS) and not offer(_,_) and not best_offer(_,_)| {
	    if B(PARTS = [PART|REST]) {
			-wants(PARTS);
			+wants(REST);
			+requested_part(PART);
			send(contractor1, cfp, part(PART));
			send(contractor2, cfp, part(PART));
			send(contractor3, cfp, part(PART));

			adoptz(wait);
			+waiting(0);
		}

    }

	wait <- waiting(TIME) | {
		if B(TIME > 50) {
			-waiting(TIME);
			if B(offer(_,_)){
				if B(requested_part(PART)){
					-requested_part(PART);
				}
				adoptz(awardContract);
			}else{
				if B(requested_part(PART)){
					+no_offers_for(PART);
					-requested_part(PART);
				}
				adoptz(announce);
			}
			dropgoal(wait);
		}else{
			Wait();
		}
	}


    % when receive proposals from all contractor agents, then manager agent
    % then compare the price and pick the cheapest bid
    % then send the msg with accept_proposal to the contrator agent with cheapest bid
    % and also send other contractor agents a reject_proposal msg.
    awardContract <- offer(CONTRACTOR, ITEM) | {
		if B(best_offer(B_CONTRACTOR,B_ITEM)){
			% the last var is always the price,
			% if there were a function that could count subvars in a term
			% then we can always extract the price. Its called functor, exraction
			% function is called
			% this only works because price is always last.
			B(functor(ITEM, _, VARCOUNT));
			B(arg(VARCOUNT, ITEM, PRICE));
			% same extraction procedure for the B item
			B(functor(B_ITEM, _, B_VARCOUNT));
			B(arg(B_VARCOUNT, B_ITEM, B_PRICE));

			if B(PRICE < B_PRICE) {
				% If we found a better one reject the previous best
				-best_offer(B_CONTRACTOR, B_ITEM);
				+best_offer(CONTRACTOR, ITEM);
			}
		}else{
			+best_offer(CONTRACTOR, ITEM);
		}
		-offer(CONTRACTOR, ITEM);
		if B((not offer(_,_)) and best_offer(B_CONTRACTOR,B_ITEM)){
			% ding ding ding, we have a winner!
			send(B_CONTRACTOR,inform,part(B_ITEM));
			adoptz(announce);
			+contract(B_CONTRACTOR, B_ITEM);
			-best_offer(B_CONTRACTOR, B_ITEM);
		}
    }

pcrules:
    % receive the proposal from a contractor
    message(CONTRACTOR,propose,La,On,offer(ITEM)) <- true | {
		+offer(CONTRACTOR, ITEM);
    }
